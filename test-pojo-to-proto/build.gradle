buildscript {
    repositories {
        maven {
            name = "local"
            url = rootProject.layout.buildDirectory.dir("repo")
        }
        mavenCentral()
    }
    dependencies {
        classpath "com.anupambasak.pojo-to-proto:plugin:0.1.1"
    }
}

plugins{
    id "com.google.protobuf" version "0.9.6"
}

ext.protobufVersion = "3.25.8"


apply plugin: 'com.anupambasak.pojo-to-proto'
apply plugin: 'java-library'

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:${protobufVersion}"
    }
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(25))
    }
}

tasks.withType(JavaCompile).configureEach {
    options.release.set(17)
}

dependencies {

    implementation "com.google.protobuf:protobuf-java:${protobufVersion}"
    implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
    implementation("com.google.api.grpc:proto-google-common-protos:2.63.2")

    implementation("com.fasterxml.jackson.core:jackson-annotations:2.18.2")
    implementation("com.google.guava:guava:33.5.0-jre")

    // If you need to IMPORT these protos in your own .proto files
    protobuf("com.google.api.grpc:proto-google-common-protos:2.63.2")

    compileOnly 'org.projectlombok:lombok:1.18.34'
    annotationProcessor 'org.projectlombok:lombok:1.18.34'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.12.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.12.1'

}



testing {
    suites {
        test {
            useJUnitJupiter('5.12.1')
            targets.all {
                testTask.configure {
                    systemProperty 'root.project.dir', rootProject.projectDir.absolutePath
                }
            }
        }
    }
}

pojoToProto {
    source.from(project.layout.projectDirectory.dir("src/main/java/com/anupambasak/gradle/dtos"))
    source.from(project.layout.projectDirectory.dir("src/main/java/com/anupambasak/gradle/testenums"))
    destination = project.layout.projectDirectory.dir("src/main/proto")
    packageName = "com.anupambasak.gradle.proto"
}

tasks.named('generateProto').configure {
    dependsOn(tasks.named('pojoToProto'))
}

tasks.named('processResources').configure {
    dependsOn(tasks.named('pojoToProto'))
}

tasks.named('test').configure {
    dependsOn(tasks.named('pojoToProto'))
}

tasks.named('clean').configure {
    delete(fileTree(layout.projectDirectory.dir("src/main/proto")) {
        include "**/*.proto"
    })
}
